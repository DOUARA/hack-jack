// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Sys.jack

/**
 * A library that supports various program execution services.
 */
class Sys {

    /** Performs all the initializations required by the OS. */
    function void init() {
        
        // Opearting System startup
        do Memory.init();
        do Math.init();
        do Keyboard.init();
        do Output.init();
        do Screen.init();
        
        // Run the Application software 
        do Main.main();
        
        do Sys.halt();

        return;
    }

    /** Halts the program execution. */
    function void halt() {
        while(true){}
        return;
    }

    /** Waits approximately duration milliseconds and returns.  */
    function void wait(int duration) {
       
        var int index, factor;
        let index = 0;
        
        /* We use nested loops instead of multiplying duration with factor 
        because multiplication result could be negative */
        while( index < duration ) { 
            let factor = 129;
           
            while( factor > 0 ) {
                let factor = factor - 1;
            }

            let index = index + 1;
        }
                  
        return;
        
    }

    /** Displays the given error code in the form "ERR<errorCode>",
     *  and halts the program's execution. */
    function void error(int errorCode) {
        do Output.printString("ERR");
        do Output.printInt(errorCode);
        do Sys.halt();
        return;
    }
}
